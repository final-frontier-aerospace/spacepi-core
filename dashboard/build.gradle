buildscript {
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.13"
    }

    repositories {
        mavenCentral()
    }
}

apply plugin: "application"
apply plugin: "com.google.protobuf"
apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "java"

group = "com.ffaero.spacepi"
version = "1.0.0"

application {
    mainClass = "com.ffaero.spacepi.dashboard.Main"
}

dependencies {
    implementation("org.lwjglx:lwjgl3-awt:0.1.7") {
        exclude group: "org.lwjgl", module: "lwjgl-opengl"
        exclude group: "org.lwjgl", module: "lwjgl"
    }
    implementation "org.lwjgl:lwjgl-opengl:3.2.3"
    implementation "org.lwjgl:lwjgl:3.2.3"
    implementation "com.google.protobuf:protobuf-java:3.13.0"
}

eclipse {
    project {
        name = "SpacePi Dashboard"
    }

    classpath {
        downloadSources = true
        downloadJavadoc = true
    }
}

idea {
    project {
        vcs = "Git"
    }

    module {
        name = "SpacePi Dashboard"
        downloadSources = true
        downloadJavadoc = true
    }
}

jar {
    manifest {
        attributes "Main-Class": "com.ffaero.spacepi.dashboard.Main"
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.0.0"
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    generated {
        java {
            srcDir "$buildDir/generated/source/proto/main/java"
        }
    }
}

task copyRuntimeLibs(type: Copy) {
    group "Distribution"
    description "Copies runtime classpath into folder so distribution scripts work properly in-tree."

    into "$buildDir/lib"
    from configurations.runtimeClasspath
    from jar.archivePath

    dependsOn jar
}

assembleDist {
    dependsOn copyRuntimeLibs
}

build {
    dependsOn copyRuntimeLibs
}
